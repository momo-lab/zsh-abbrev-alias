#!/usr/bin/env zsh

typeset -g _ABBREV_ALIAS_VERSION='0.3.0'
typeset -g _ABBREV_ALIAS_RECURSIVE_LIMIT=8

if [ "${YSU_VERSION+exists}" ]; then
  typeset -agUx YSU_IGNORED_GLOBAL_ALIASES
  typeset -agUx YSU_IGNORED_ALIASES

  __remove_item_from_array() {
    local array_name=$1
    local item=$2
    eval "local -a array=(\${${array_name}[@]})"
    local new_array
    for i in $array; do
      if [[ "$i" != "$item" ]]; then
        new_array+=($i)
      fi
    done
    eval "${array_name}=(\${new_array[@]})"
  }
fi

typeset -Ag _abbrev_aliases

__abbrev_alias::recursive_command() {
  local counter=$1; shift
  if [[ $counter -ge $_ABBREV_ALIAS_RECURSIVE_LIMIT ]]; then
    echo "$*"
    return
  fi

  local kind=$1; shift
  local result=""
  while [ "$1" != "" ]; do
    local abbr=${_abbrev_aliases[$1]}
    local value=${${(s.\0.)abbr}[2]:-$1}
    local abbr_kind=${${(s.\0.)abbr}[1]}
    if [[ "$kind" == "$abbr_kind" ]]; then
      if [[ "$1" != "$value" ]]; then
        local is_recursive=${${(s.\0.)abbr}[4]}
        if [[ "$is_recursive" == "1" ]]; then
          value=$(__abbrev_alias::recursive_command $(($counter+1)) $abbr_kind ${=value})
        fi
      fi
    else
      value=$1
    fi
    result+=" $value"
    shift
  done
  echo "${result:1}"
}

__abbrev_alias::magic_abbrev_expand() {
  emulate -L zsh -o extended_glob
  local MATCH
  LBUFFER=${LBUFFER%%(#m)[-_a-zA-Z0-9#%+.~,:/]#}
  local abbr=${_abbrev_aliases[$MATCH]}
  local kind=${${(s.\0.)abbr}[1]}
  local newbuffer=${${(s.\0.)abbr}[2]:-$MATCH}
  if [[ $kind == "c" && ! ( $LBUFFER == "" || $LBUFFER =~ "(\\||;|&|\\$\\(|\`) ?$" ) ]]; then
    newbuffer=$MATCH
  else
    local is_eval=${${(s.\0.)abbr}[3]}
    if [[ "$is_eval" == "1" ]]; then
      newbuffer=$(eval "echo \"$newbuffer\"")
    fi
    local is_recursive=${${(s.\0.)abbr}[4]}
    if [[ "$is_recursive" == "1" ]]; then
      newbuffer=$(__abbrev_alias::recursive_command 0 $kind ${=newbuffer})
    fi
  fi
  LBUFFER+=$newbuffer
}

__abbrev_alias::magic_abbrev_expand_and_insert() {
    zle __abbrev_alias::magic_abbrev_expand
    zle self-insert
}

__abbrev_alias::magic_abbrev_expand_and_accept_line() {
    zle __abbrev_alias::magic_abbrev_expand
    zle accept-line
}

__abbrev_alias::no_magic_abbrev_expand() {
  LBUFFER+=' '
}

__abbrev_alias::init() {
  zle -N __abbrev_alias::magic_abbrev_expand
  zle -N __abbrev_alias::no_magic_abbrev_expand
  zle -N __abbrev_alias::magic_abbrev_expand_and_insert
  zle -N __abbrev_alias::magic_abbrev_expand_and_accept_line
  bindkey " "    __abbrev_alias::magic_abbrev_expand_and_insert
  bindkey "^M"   __abbrev_alias::magic_abbrev_expand_and_accept_line
  bindkey "^x "  __abbrev_alias::no_magic_abbrev_expand
  bindkey "^x^M" accept-line
}

__abbrev_alias::show() {
  local kind_filter=$1
  local key=$2
  local abbr=${_abbrev_aliases[$key]}
  test -n "$abbr" || return 1
  local kind=${${(s.\0.)abbr}[1]}
  local value=${${(s.\0.)abbr}[2]}
  if [[ $kind_filter == $kind ]]; then
    echo "$key='$value'"
  fi
  return 0
}

__abbrev_alias::list() {
  local kind_filters=$1
  for kind_filter in ${(s::)kind_filters}; do
    for key in ${(ko)_abbrev_aliases}; do
      __abbrev_alias::show $kind_filter $key
    done
  done
}

__abbrev_alias::version() {
  echo "abbrev-alias ${_ABBREV_ALIAS_VERSION}"
}

__abbrev_alias::help() {
  __abbrev_alias::version
  echo "USAGE:"
  echo "  abbrev-alias [OPTIONS] {name=value ...}"
  echo "  abbrev-alias -u {name ...}"
  echo "  abbrev-alias --init"
  echo
  echo "OPTIONS:"
  echo "  -c, --command   register alias as 'alias name=value'"
  echo "  -g, --global    register alias as 'alias -g name=value' like"
  echo "  -e, --eval      evaluates subshells on expansion"
  echo "  -r, --recursive expand aliases recursively"
  echo "  -u, --unset     unregister alias"
  echo "  -i, --init      initialize abbrev-alias. execute with .zshrc"
  echo "  -h, --help      show this help"
  echo "  -v, --version   show version"
}

__abbrev_alias::unregist() {
  local key=$1
  if [[ -n "${_abbrev_aliases[$key]}" ]]; then
    unalias $key
    unset "_abbrev_aliases[$key]"
    if [ "${YSU_VERSION+exists}" ]; then
      __remove_item_from_array YSU_IGNORED_GLOBAL_ALIASES $key
      __remove_item_from_array YSU_IGNORED_ALIASES $key
    fi
  else
    echo "no such alias: $key" >&2
    return 1
  fi
}

__abbrev_alias::regist() {
  local kind=$1 key=${2%%=*} value=${2#*=} is_eval=$3 is_recursive=$4
  if [[ "$kind" == "f" ]]; then
    echo "WARN: -f option is deprecated. Use -g -e options." 1>&2
    echo "      ex) abbrev-alias -g -e $key='\$($value)'" 1>&2
    value="\$($value)"
    is_eval=1
    kind=g
  fi
  case "$kind" in
    g) alias -g $key="$value";;
    c) alias    $key="$value";;
  esac

  if [ "${YSU_VERSION+exists}" ]; then
    if [[ "$kind" == "g" ]]; then
      YSU_IGNORED_GLOBAL_ALIASES+=($key)
    else
      YSU_IGNORED_ALIASES+=($key)
    fi
  fi
  _abbrev_aliases[$key]="$kind\0$value\0$is_eval\0$is_recursive"
}

# option parse & execute
local -A opts
zparseopts -D -M -A opts -- \
  -init i=-init \
  -help h=-help \
  -version v=-version \
  -unset u=-unset \
  -command c=-command \
  -global g=-global \
  -function f=-function \
  -eval e=-eval \
  -recursive r=-recursive

# init
if [[ -n ${opts[(i)--init]} ]]; then
  __abbrev_alias::init
  return 0
fi

# help
if [[ -n ${opts[(i)--help]} ]]; then
  __abbrev_alias::help
  return 0
fi

# version
if [[ -n ${opts[(i)--version]} ]]; then
  __abbrev_alias::version
  return 0
fi

# unregist
if [[ -n ${opts[(i)--unset]} ]]; then
  local result=0
  while [ $# -gt 0 ]; do
    __abbrev_alias::unregist "$1"
    test $? -ne 0 && result=1
    shift
  done
  return $result
fi

# set options
local kind
if [[ -n ${opts[(i)--command]} ]]; then
  kind=c
elif [[ -n ${opts[(i)--global]} ]]; then
  kind=g
elif [[ -n ${opts[(i)--function]} ]]; then
  kind=f
fi 
[[ ! -n ${opts[(i)--eval]} ]]; local is_eval=$?
[[ ! -n ${opts[(i)--recursive]} ]]; local is_recursive=$?

#list
if [ $# -eq 0 ]; then
  __abbrev_alias::list ${kind:-gc}
  return 0
fi

# regist or show
local result=0
while [ $# -gt 0 ]; do
  if [[ "$1" =~ "=" ]]; then
    __abbrev_alias::regist ${kind:-c} "$1" $is_eval $is_recursive
  else
    __abbrev_alias::show ${kind:-c} $1
    test $? -ne 0 && result=1
  fi
  shift
done
return $result
