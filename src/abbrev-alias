#!/usr/bin/env zsh

typeset -g _ABBREV_ALIAS_VERSION='0.1.0'

typeset -Ag _abbrev_aliases

__abbrev_alias::magic_abbrev_expand() {
  local MATCH
  LBUFFER=${LBUFFER%%(#m)[-_a-zA-Z0-9]#}
  local abbr=${_abbrev_aliases[$MATCH]:-  $MATCH}
  local kind=${abbr[1,1]}
  local is_eval=${abbr[2,2]}
  local newbuffer=${abbr[3,-1]}
  if [[ $kind == "c" && $LBUFFER != "" ]]; then
    newbuffer=$MATCH
  fi
  if [[ "$is_eval" == "1" ]]; then
    newbuffer=$(eval "echo \"$newbuffer\"")
  fi
  LBUFFER+=$newbuffer
  zle self-insert
}

__abbrev_alias::no_magic_abbrev_expand() {
  LBUFFER+=' '
}

__abbrev_alias::init() {
  setopt extended_glob
  zle -N __abbrev_alias::magic_abbrev_expand
  zle -N __abbrev_alias::no_magic_abbrev_expand
  bindkey " "   __abbrev_alias::magic_abbrev_expand
  bindkey "^x " __abbrev_alias::no_magic_abbrev_expand
}

__abbrev_alias::list() {
  local kind_filter=$1
  local key value kind
  for key in ${(k)_abbrev_aliases}; do
    value=${_abbrev_aliases[$key]}
    kind=${value[1,1]}
    value=${value[3,-1]}
    if [[ $kind_filter == a || $kind_filter == $kind ]]; then
      echo "$key='$value'"
    fi
  done
}

__abbrev_alias::version() {
  echo "abbrev-alias ${_ABBREV_ALIAS_VERSION}"
}

__abbrev_alias::help() {
  __abbrev_alias::version
  echo "usage: abbrev-alias [OPTIONS] {name=value ...}"
  echo "       abbrev-alias -u {name ...}"
  echo "       abbrev-alias --init"
  echo
  echo "options:"
  echo "  -c, --command   register alias as 'alias name=value'"
  echo "  -g, --global    register alias as 'alias -g name=value'"
  echo "  -e, --eval      evaluates subshells on expansion. "
  echo "  -u, --unset     unregister alias"
  echo "  -i, --init      initialize abbrev-alias. execute with .zshrc"
  echo "  -h, --help      show this help"
  echo "  -v, --version   show version"
}

__abbrev_alias::unregist() {
  local key=$1
  if [[ -n "${_abbrev_aliases[$key]}" ]]; then
    unalias $key
    unset "_abbrev_aliases[$key]"
  else
    echo "no such alias: $key" >&2
  fi
}

__abbrev_alias::regist() {
  local kind=$1 is_eval=$2 key=${3%%=*} value=${3#*=}
  if [[ "$kind" == "f" ]]; then
    echo "WARN: -f option is deprecated. Use -g -e options." 1>&2
    echo "      ex) abbrev-alias -g -e $key='\$($value)'" 1>&2
    value="\$($value)"
    is_eval=1
    kind=g
  fi
  case "$kind" in
    g) alias -g $key="$value";;
    c) alias    $key="$value";;
  esac
  _abbrev_aliases[$key]="$kind$is_eval$value"
}

# option parse & execute
local kind
local is_eval=0
local -a args
while [ $# -gt 0 ]; do
  case "$1" in
    -i|--init)      __abbrev_alias::init; return;;
    -h|--help)      __abbrev_alias::help; return;;
    -v|--version)   __abbrev_alias::version; return;;
    -u|--unset)     kind=u;;
    -c|--command)   kind=c;;
    -g|--global)    kind=g;;
    -f|--function)  kind=f;;
    -e|--eval)      is_eval=1;;
    *) args=("${args[@]}" $1) ;;
  esac
  shift
done

local value
if [[ $#args == 0 ]]; then
  __abbrev_alias::list ${kind:-a}
  return
fi
if [[ "$kind" == "u" ]]; then
  for value in ${args[@]}; do
    __abbrev_alias::unregist $value
  done
else
  for value in ${args[@]}; do
    __abbrev_alias::regist ${kind:-c} $is_eval $value
  done
fi
